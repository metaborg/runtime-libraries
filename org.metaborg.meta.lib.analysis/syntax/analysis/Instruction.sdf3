module analysis/Instruction

imports
  
  analysis/Task
  analysis/Term  

context-free syntax

  MsgType.Note = <note> 
  MsgType.Warn = <warning> 
  MsgType.Err = <error> 
  MsgTrigger.Fail = <fails> 
  MsgTrigger.Succ = <succeeds> 
  MsgTrigger.Mult = <has multiple results> 
  Instruction.Msg = <show <MsgType> <Term> on <Term> when <Term> <MsgTrigger>> 
  Instruction.Choice = <choose <{Term " <+ "}*>> 
  Instruction.Sequence = <sequence <{Term " ; "}*>> 
  Instruction.Combine = <concat <{Term " + "}*>> 
  Instruction.Id = <produce <Term>> 
  Instruction.PropLookup = <lookup <Term> prop on <Term>> 
  Instruction.PropsLookup = <lookup <Term> props on <Term>> 
  Instruction.PropCheck = <check <Term> prop <Term> against <Term> wrt <Term>> 
  Instruction.Rewrite = <rewrite <Term> wrt <Term>> 
  Instruction.Match = <match <Term> using relation <Term> wrt bindings <Term>> 
  Instruction.ResolveDefs = <resolve <Term> <Term> in <Term> wrt props <Term>> 
  Instruction.CheckDefs = <check <Term> against namespace <Term>> 
