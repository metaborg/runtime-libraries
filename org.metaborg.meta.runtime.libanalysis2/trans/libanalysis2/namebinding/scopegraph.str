module libanalysis2/namebinding/scopegraph

imports
  src-gen/signatures/libanalysis2/-
  libanalysis2/pp
  libanalysis2/prelude/-

signature
  sorts
    Graph
    ScopeImpl

  constructors
    G : List((Scope,ScopeImpl)) * List((DeclId,Scope)) -> Graph
    S : OptionScope * List(DeclId) * List(RefId) * List(RefId) * List(Scope) -> ScopeImpl

rules

  G-new = !G([],[])
  G--new_s = !S(NoneScope(),[],[],[],[])
  G-with-parent   = ?(G(s*,a*),s,p)  ; !G(<update(?S(_,d*,r*,ri*,si*);!S(p,d*,r*,ri*,si*)      ,G--new_s)> (s,s*), a*)
  G-with-decl     = ?(G(s*,a*),s,d)  ; !G(<update(?S(p,d*,r*,ri*,si*);!S(p,[d|d*],r*,ri*,si*)  ,G--new_s)> (s,s*), a*)
  G-with-ref      = ?(G(s*,a*),s,r)  ; !G(<update(?S(p,d*,r*,ri*,si*);!S(p,d*,[r|r*],ri*,si*)  ,G--new_s)> (s,s*), a*)
  G-with-r-import = ?(G(s*,a*),s,r)  ; !G(<update(?S(p,d*,r*,ri*,si*);!S(p,d*,r*,[r|ri*],si*)  ,G--new_s)> (s,s*), a*)
  G-with-s-import = ?(G(s*,a*),s,s') ; !G(<update(?S(p,d*,r*,ri*,si*);!S(p,d*,r*,ri*,[s'|si*]) ,G--new_s)> (s,s*), a*)
  G-with-assoc    = ?(G(s*,a*),d,s)  ; !G(s*                                                                     , [(d,s)|a*])

  G-refs             = ?G(<id>,_) ; map(?(_,<id>);?S(_,_,<id>,_,_)) ; concat
  G-scope            = ?(G(<id>,_),r) ; getfirst(G-scope_exists(|r))
  G-scope_exists(|r) = ?(s,S(_,_,r*,_,_)) ; <elem> (r,r*) ; !s
  G-parent           = ?(G(s*,_),s) ; <lookup> (s,s*) ; ?S(SomeScope(<id>),_,_,_,_)
  G-decls            = ?(G(s*,_),s) ; <lookup> (s,s*) ; ?S(_,<id>,_,_,_)
  G-refs             = ?(G(s*,_),s) ; <lookup> (s,s*) ; ?S(_,_,<id>,_,_)
  G-r-imports        = ?(G(s*,_),s) ; <lookup> (s,s*) ; ?S(_,_,_,<id>,_)
  G-s-imports        = ?(G(s*,_),s) ; <lookup> (s,s*) ; ?S(_,_,_,_,<id>)
  G-assoc            = ?(G(_,a*),d) ; <lookup> (d,a*)

rules
  
  pp-Graph = G(map((pp-Analysis-string,pp-ScopeImpl)),map((pp-Analysis-string,pp-Analysis-string)))
  pp-ScopeImpl = S(pp-Analysis-string,map(pp-Analysis-string),map(pp-Analysis-string),map(pp-Analysis-string),map(pp-Analysis-string))
