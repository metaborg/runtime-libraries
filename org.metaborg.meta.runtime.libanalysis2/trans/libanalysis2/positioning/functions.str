module libanalysis2/positioning/functions

imports
  runtime/editor/annotations
  runtime/editor/origins
  editor-common.generated

  libanalysis2/prelude/-
  src-gen/signatures/libanalysis2/-

rules
  
  annotate-keys =
    where(
      dr-begin-scope(|"NewKey");
      key-counter := <new-counter>;
      rules( NewKey: _ -> <next-counter> key-counter )
    );
    topdown(annotate-key);
    where(
      try(dr-end-scope(|"NewKey"))
    )

  annotate-key: t -> t{<NewKey>}
  
rules

  get-position: Var(_) -> POfDecl(<id>)
  
  get-position: DeclId(_, _, p) -> p
  
  get-position: t{a*} -> Pos(k,f,l,c,s,e)
  where <getfirst(is-int)> a* => k
  with (origin-location-offset <+ debug(!"WARNING: Cannot get origin for ") ; !("",-1,-1,-1,-1)) => (f,l,c,s,e)

  position-to-origin:
    Pos(_,f,l,c,s,e) -> (f,l,c,s,e)

  position-to-key:
    Pos(k,_,_,_,_,_) -> k

  position-fix = where(position-to-origin => o) ; try(origin-location-offset-set(|o))

rules
 
  apply-annotations(s|aa*): ast -> ast'
  with <topdown(try(apply-annotations-to-term(s|aa*)))> ast => ast'

  apply-annotations-to-term(s|aa*): t{a*} -> t{a''*}
  where a'* := <filter(<lookup> (<id>,aa*));map(s)> a*;
        a''* := [a*,a'*]