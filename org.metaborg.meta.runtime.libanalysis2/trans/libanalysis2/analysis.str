module libanalysis2/analysis

imports
  signatures/libanalysis2/-
  libanalysis2/abstract-syntax/-
  libanalysis2/extraction/-
  libanalysis2/namebinding/-
  libanalysis2/typechecking/-
  libanalysis2/solver/-
  libanalysis2/prelude/-
  libanalysis2/pp

rules

  analyse: desugared-ast -> ( (analysed-ast, errors*, warnings*, notes*)
                            , [ ("constraints",c*)
                              , ("scopegraph",g)
                              , ("solutions", sol*)
                              ])
  with
    (annotated-ast,c*) := <extract-constraints> desugared-ast
  ; sol-i := <initial-solution>
  ; [(sol-wm1,c1*)] := <solve-constraints-phase1> (sol-i,c*)
  ; g := <fromWM;sol-g> sol-wm1
  ; <solve-constraints-phase2;map(postprocess-errors)> (sol-wm1,c1*) => sol-wm*
  ; map( ![ ("final-scopegraph", <fromWM;sol-g>)
          , ("resolution", <fromWM;sol-r>)
          , ("sub-types", <fromWM;sol-x>)
          , ("pos-types", <fromWM;sol-p>)
          , ("subst", <fromWM;sol-s>)
          , ("lower-bounds", <fromWM;sol-lb>)
          , ("errors", <wm-errors>)
          , ("warnings", <wm-warnings>)
          , ("notes", <wm-notes>)
          ]
       ) => sol*
  ; <partition(not(is-erronious))> sol-wm* => (good-wm*, bad-wm*)
  ; n := (None(),$[Found [<length> good-wm*] correct and [<length> bad-wm*] erronious solutions.])
  ; ( ( <Hd> good-wm* <+ <Hd> bad-wm* ) => sol-wm
    < <fromWM;sol-r> sol-wm => r*
    ; resolved-ast := <apply-resolution(|r*)> annotated-ast
    ; <fromWM;sol-p> sol-wm => p*
    ; typed-ast := <apply-types(|p*)> resolved-ast
    ; analysed-ast := typed-ast
    ; errors* := <wm-errors;mapv(xmlencode)> sol-wm
    ; warnings* := <wm-warnings;mapv(xmlencode)> sol-wm
    ; notes* := [n,<wm-notes;mapv(xmlencode)> sol-wm]
    + analysed-ast := annotated-ast
    ; errors* := [(None(),"Error during analysis")]
    ; warnings* := []
    ; notes* := [n]
    )

  pp-analysis: [
    ("constraints",c*)
  , ("scopegraph",g)
  , ("solutions", sol*)
  ] -> [
    ("constraints",<filter(not(irrelevant-constraint));map(pp-Analysis-string)> c*)
  , ("scopegraph", <pp-Graph> g)
  , ("solutions", <map(pp-analysis-solution)> sol*)
  ]

  pp-analysis-solution: [
    ("final-scopegraph", g)
  , ("resolution", r*)
  , ("sub-types", x*)
  , ("pos-types", p*)
  , ("subst", s*)
  , ("lower-bounds", lb*)
  , ("errors", e*)
  , ("warnings", w*)
  , ("notes", n*)
  ] -> [
    ("final-scopegraph", <pp-Graph> g)
  , ("resolution", <map(tapp(pp-Analysis-string,pp-Analysis-string))> r*)
  , ("sub-types", <pp-SubtypeRelation> x*)
  , ("pos-types", <filter(not(irrelevant-pos));map(tapp(pp-Analysis-string,pp-Analysis-string))> p*)
  , ("subst", <map(tapp(pp-Analysis-string,pp-Analysis-string))> s*)
  , ("lower-bounds", <map(tapp(pp-Analysis-string,pp-Analysis-string))> lb*)
  , ("errors", <map(tapp(pp-Analysis-string,id))> e*)
  , ("warnings", <map(tapp(pp-Analysis-string,id))> w*)
  , ("notes", <map(tapp(pp-Analysis-string,id))> n*)
  ]
    
  irrelevant-constraint = fail
  irrelevant-pos = fail
