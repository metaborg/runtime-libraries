module libanalysis2/nabl-ts/nabl-ts

imports
  
  runtime/editor/origins
  runtime/editor/annotations
  src-gen/signatures/libanalysis2/-
  libanalysis2/pp

rules
  
  nabl-ts-constraint(|tvars, facts, constraints, scopes) = fail

rules

  do-extract-constraint =
    ?ast;
    // Ensure all dynamic rules are properly scoped
    try(dr-scope-all-end);
    dr-scope-all-start;
    scope-counter := <new-counter>;
    var-counter := <new-counter>;
    rules( NewScope: _ -> <next-counter> scope-counter
           NewVar: _ -> <next-counter> var-counter
         );
    facts := <new-iset>;
    constraints := <new-iset>;
    tvars := <new-hashtable>;
    ast' := <
    	annotate-keys; 
      nabl-ts-traverse(|tvars, facts, constraints, [Global()])> ast;
    f* := <iset-elements> facts;
    c* := <iset-elements> constraints;
    try(dr-scope-all-end);
    !(ast', [f*, c*])

  annotate-keys = topdown(annotate-key)
 
  annotate-key: t -> t{<newname> ""}
  
  nabl-ts-traverse(|tvars, facts, constraints, scopes) =
     debug(!"enter "); nabl-ts-constraint(|tvars, facts, constraints, scopes); debug(!"custom rule successful ")
  <+ debug(!"custom rule failed "); nabl-ts-traverse-children(|tvars, facts, constraints, scopes); debug(!"generic rule successful ")
  <+ <debug(!"generic rule failed ")> scopes ; fail
  
  nabl-ts-traverse-children(|tvars, facts, constraints, scopes) =
    with(
    	get-arguments;
    	map(nabl-ts-traverse(|tvars, facts, constraints, scopes))
    )
  
  get-type-constraint(|tvars, tp, msg):
    Var(_) -> CEqType(<id>, tp, msg)
    
  get-type-constraint(|tvars, tp, msg):
    _ -> CEqType(<get-type-var(|tvars)>, tp, msg)
      
  get-type-var(|tvars) =
     [key] := <get-annos>
   ; (    tvar := <hashtable-get(|key)> tvars 
       <+ tvar := Var(<NewVar>)
        ; <hashtable-put(|key, tvar)> tvars
     )
   ; !tvar
  <+ debug(!"missing tvar "); fail
 