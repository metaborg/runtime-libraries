module libanalysis2/abstract-syntax/abstract-syntax

imports
  runtime/editor/annotations
  runtime/editor/origins
  editor-common.generated

  libanalysis2/prelude/-
  src-gen/signatures/libanalysis2/-

rules

  get-position: Var(_) -> POfDecl(<id>)
  
  get-position: t{k} -> Pos(k,f,l,c,s,e)
  with <is-string> k <+ <debug;fail> $[ERR: Cannot get key for [<write-to-string> t]]
  where (f,l,c,s,e) := <origin-location-offset> t
    <+ <debug;fail> $[ERR: Cannot get position for [<write-to-string> t]]

  position-to-origin:
    Pos(_,f,l,c,s,e) -> (f,l,c,s,e)

  position-to-key:
    Pos(k,_,_,_,_,_) -> k

rules
 
  apply-annotations(s|aa*): ast -> ast'
  with <topdown(try(apply-annotations-to-term(s|aa*)))> ast => ast'

  apply-annotations-to-term(s|aa*): t{a*} -> t{a''*}
  where a'* := <filter(<lookup> (<id>,aa*));map(s)> a*;
        a''* := [a*,a'*]