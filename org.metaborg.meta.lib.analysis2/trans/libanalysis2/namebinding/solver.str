module libanalysis2/namebinding/solver

imports
  
  libanalysis2/namebinding/-
  libanalysis2/prelude/-
  libanalysis2/solver/-
  libanalysis2/pp
  signatures/libanalysis2/-

rules
 
  solve-fact: (FDecl(d,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-decl> (g,s,d) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-fact: (FRef(r,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-ref> (g,s,r) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-fact: (FImportR(r,l,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-r-import> (g,s,(r,l)) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-fact: (FImportS(s',l,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-s-import> (g,s,(s',l)) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-fact: (FParent(s,p),sol-wm)   -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-parent> (g,s,p) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-fact: (FAssoc(d,s),sol-wm)    -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-assoc> (g,d,s) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

rules

  solve-constraint: (CResolves(r,l,d,msginfo),sol-wm) -> [(sol-wm,c*)]
  where <fromWM;sol-r> sol-wm => r*
      ; <lookup> (r,r*) => (d',l')
  with msg := ["Unable to resolve ",r," to ",d]
     ; msginfo' := <default-message(|msg)> msginfo
     ; c* := [ CEqual(d,d',msginfo'),
               CEqual(l,l',msginfo')
             ]

  solve-constraint: (CResolves(r,l,d,msginfo),sol-wm) -> res*
  where <fromWM;sol-r> sol-wm => r*
      ; <not(lookup)> (r,r*)
      ; <fromWM;sol-g> sol-wm => g
      ; <resolve(|g)> r => Total(dl*@[_|_])
  with msg := ["Unable to resolve ",r," to ",d]
     ; msginfo' := <default-message(|msg)> msginfo
     ; <map(solve-namebinding-constraint-step(|r,l,d,msginfo',sol-wm))> dl* => res*

  solve-namebinding-constraint-step(|r,l,d,msginfo,sol-wm): (d',l') -> (sol-wm',c*)
  with <fmap(sol-set-r(|r,(d',l')))> sol-wm => sol-wm'
  with c* := [ CEqual(d,d',msginfo)
             , CEqual(l,l',msginfo)
             ]
 
  solve-constraint: (CAssoc(d,s,msginfo),sol-wm) -> [(sol-wm,c*)]
  where <is-ground> d
      ; <fromWM;sol-g> sol-wm => g
      ; <G-assoc> (g,d) => s'
  with msg := ["Cannot find scope ",s," associated with ",d]
     ; c* := [ CEqual(s,s',<default-message(|msg)> msginfo) ]
 
  solve-constraint: (CDistinct(s,msginfo),sol-wm) -> [(sol-wm',[])]
  where <is-ground> s
      ; <sol-g> sol-wm => g
      ; <G-decls;map(atom)> (g,s) => d*
  with ( <dups> d* => d'*
       < msg := ["Duplicate declarations ",d'*]
       ; <add-message(|msginfo,msg)> sol-wm
       + !sol-wm
       ) => sol-wm'

  solve-constraint: (CInit(s,msginfo),sol-wm) -> [(sol-wm',[])]
  where <is-ground> s
      ; <sol-g> sol-wm => g
      ; <G-refs;map(atom)> (g,s) => r*
      ; <env_v(|g,[],[]);fmap(keys)> s => Total(d*)
  with <isect> (r*,d*) => i*
     ; ( ?[]
       < !sol-wm
       + msg := ["Mismatch ",i*]
       ; <add-message(|msginfo,msg)> sol-wm
       ) => sol-wm'

rules
 
  unsolved-message: CResolves(r,l,d,msginfo) -> <default-message(|msg)> msginfo
  with msg := ["Cannot resolve ",r," to ",d]

  unsolved-message: CAssoc(d,s,msginfo) -> <default-message(|msg)> msginfo
  with msg := ["Cannot find associated scope ",s," of ",d]
