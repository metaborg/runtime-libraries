module libanalysis2/namebinding/solver

imports
  
  libanalysis2/namebinding/-
  libanalysis2/prelude/-
  libanalysis2/positioning/-
  libanalysis2/solver/-
  libanalysis2/pp
  signatures/libanalysis2/equality/-
  signatures/libanalysis2/namebinding/-
  signatures/libanalysis2/solver/-

rules
 
  solve-fact = solve-namebinding-fact 
 
  solve-namebinding-fact: (FDecl(d,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-decl> (g,s,d) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-namebinding-fact: (FRef(r,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-ref> (g,s,r) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-namebinding-fact: (FDirectEdge(l,s',t,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-direct-edge> (g,s,(l,s',t)) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-namebinding-fact: (FNamedEdge(l,r,t,s),sol-wm) -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-named-edge> (g,s,(l,r,t)) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  solve-namebinding-fact: (FAssoc(d,s),sol-wm)    -> [(sol-wm',[])]
  with <fromWM;sol-g> sol-wm => g
     ; <G-with-assoc> (g,d,s) => g'
     ; <fmap(<sol-with-g> (g',<id>))> sol-wm => sol-wm'

  constraint-priority: FDecl(_,_) -> 3
  constraint-priority: FRef(_,_) -> 3
  constraint-priority: FDirectEdge(_,_,_,_) -> 3
  constraint-priority: FNamedEdge(_,_,_,_) -> 3
  constraint-priority: FAssoc(_,_) -> 3

rules

  solve-constraint = solve-namebinding-constraint

  solve-namebinding-constraint: (CResolves(r,a,d,msginfo),sol-wm) -> [(sol-wm,c*)]
  where r* := <fromWM;sol-r> sol-wm
      ; dp := <lookup> (r,r*)
  with ( None() := dp
       < ![]
       + (d',p) := dp
       ; msg := ["Cannot resolve ",r," to ",d]
       ; msginfo' := <default-message(|msg)> msginfo
       ; a' := <path-annos> p
       ; ![ CEqual(d,d',msginfo'),
            CEqual(a,a',msginfo')
          ]
       ) => c*

  solve-namebinding-constraint: (CResolves(r,l,d,msginfo),sol-wm) -> res*
  where r* := <fromWM;sol-r> sol-wm
      ; <not(lookup)> (r,r*)
      ; g := <fromWM;sol-g> sol-wm
      ; dl* := <resolve(|g)> r
  with ( [] := dl*
       < msg := ["Missing declaration for ",r]
       ; sol-wm' := <fmap(sol-set-r(|r,None()))
                    ;add-message(detail-message(|msg)|msginfo)> sol-wm
       ; ![ (sol-wm',[]) ]
       + msg := ["Problem resolving ",r," to ",d]
       ; msginfo' := <default-message(|msg)> msginfo
       ; <map(solve-namebinding-constraint-step(|r,l,d,msginfo',sol-wm))> dl*
       ) => res*

  solve-namebinding-constraint-step(|r,a,d,msginfo,sol-wm): (d',p) -> (sol-wm',c*)
  with <fmap(sol-set-r(|r,(d',p)))> sol-wm => sol-wm'
  with a' := <path-annos> p
  with c* := [ CEqual(d,d',msginfo)
             , CEqual(a,a',msginfo)
             ]
 
  solve-namebinding-constraint: (CAssoc(d,s,msginfo),sol-wm) -> [res]
  where <is-ground> d
      ; g := <fromWM;sol-g> sol-wm
  with ( s' := <G-assoc> (g,d)
       < msg := ["Problem associating scope ",s," with ",d]
       ; !(sol-wm,[CEqual(s,s',<default-message(|msg)> msginfo)])
       + msg := [d," is not associated with a scope"]
       ; sol-wm' := <add-message(detail-message(|msg)|msginfo)> sol-wm
       ; !(sol-wm',[])
       ) => res
 
  solve-namebinding-constraint: (CDistinct(N,msginfo),sol-wm) -> [(sol-wm',[])]
  where <is-ground> N
      ; g := <fromWM;sol-g> sol-wm
      ; n* := <get-names(|g)> N
  with n'* := <dups(id-eq)> n*
     ; ( ?[]
       < !sol-wm
       + msg := "Duplicate name"
       ; <add-message(default-message(|msg)|<default-names-pos(|n'*)> msginfo)> sol-wm
       ) => sol-wm'

  solve-namebinding-constraint: (CSubset(N1,N2,msginfo),sol-wm) -> [(sol-wm',[])]
  where <is-ground> N1
      ; <is-ground> N2
      ; g := <fromWM;sol-g> sol-wm
      ; n1* := <get-names(|g)> N1
      ; n2* := <get-names(|g)> N2
  with n'* := <diff(id-eq)> (n1*,n2*)
     ; ( ?[]
       < !sol-wm
       + msg := "Missing name"
       ; <add-message(default-message(|msg)|<default-names-pos(|n'*)> msginfo)> sol-wm
       ) => sol-wm'

  solve-namebinding-constraint: (CDisjoint(N1,N2,msginfo),sol-wm) -> [(sol-wm',[])]
  where <is-ground> N1
      ; <is-ground> N2
      ; g := <fromWM;sol-g> sol-wm
      ; n1* := <get-names(|g)> N1
      ; n2* := <get-names(|g)> N2
  with n'* := <isect(id-eq)> (n1*,n2*)
     ; ( ?[]
       < !sol-wm
       + msg := "Duplicate name"
       ; <add-message(default-message(|msg)|<default-names-pos(|n'*)> msginfo)> sol-wm
       ) => sol-wm'

rules

  constraint-priority: CAssoc(_,_,_) -> 4
  constraint-priority: CResolves(_,_,_,_) -> 4
  constraint-priority: CDistinct(_,_) -> 4
  constraint-priority: CSubset(_,_,_) -> 4
  constraint-priority: CDisjoint(_,_,_) -> 4

rules
 
  default-names-pos(|n*) =
    try(Message(id,?None(),!NAMES()))
  ; Message(id,bottomup(try(?NAMES();<map(id-name)> n*)),try(?NAMES();<map(position-to-term)> n*))
 
  unresolved-refs(|rn*): sol-wm -> sol-wm'
    with sol := <fromWM> sol-wm
       ; ref* := <sol-g;G-refs> sol
       ; res* := <sol-r;keys> sol
       ; ar* := <diff> (<keys> rn*, res*)
       ; ur* := <diff> (ref*,[res*,ar*])
       ; sol-wm' := <foldr(!sol-wm,{ ?(r,<id>) ; add-message(id|Message(Error(),["Failed to resolve ",r],<position-to-term> r)) })> ur*
 
  constraint-message: CResolves(r,l,d,msginfo) -> <default-message(|["Failed to resolve ",r])> msginfo

  constraint-message: CAssoc(d,s,msginfo) -><default-message(|["Failed to find associated scope of ",d])> msginfo

  constraint-message: CDistinct(n,msginfo) -><default-message(|["Failed to check if names in ",n," are distinct"])> msginfo

  constraint-message: CSubset(n1,n2,msginfo) -><default-message(|["Failed to check if names in ",n1," are in in ",n2])> msginfo

  constraint-message: CDisjoint(n1,n2,msginfo) -><default-message(|["Failed to check if names in ",n1," and ",n2," are disjoint"])> msginfo
