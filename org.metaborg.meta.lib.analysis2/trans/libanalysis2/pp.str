module libanalysis2/pp

imports

  libstratego-gpp
  runtime/tmpl/pp
  include/libanalysis2-parenthesize
  pp/libanalysis2/-
  libanalysis2/pp/overrides

rules

  pp-Analysis(pp) =
      parenthesize-libanalysis2
    ; (pp <+ debug(!"WARNING: Could not pp ") ; prettyprint-Term)
    ; !V([], <id>)
    ; box2text-string(|120)

  pp-DeclId               = pp-Analysis(prettyprint-DeclId)
  pp-RefId                = pp-Analysis(prettyprint-RefId)
  pp-Atom                 = pp-Analysis(prettyprint-Atom)
  pp-Scope                = pp-Analysis(prettyprint-Scope)
  pp-OptionScope          = pp-Analysis(prettyprint-OptionScope)
  pp-Pos                  = pp-Analysis(prettyprint-Pos)
  pp-Var                  = pp-Analysis(prettyprint-Var)
  pp-Constraint           = pp-Analysis(prettyprint-Constraint)
  pp-Type                 = pp-Analysis(prettyprint-Type)
  pp-TypeDef              = pp-Analysis(prettyprint-TypeDef)
  pp-TypeArg              = pp-Analysis(prettyprint-TypeArg)
  pp-TypeCon              = pp-Analysis(prettyprint-TypeCon)
  pp-Message              = pp-Analysis(prettyprint-Message)
  pp-MessageContent       = pp-Analysis(prettyprint-MessageContent)
  pp-Substitution         = pp-Analysis(prettyprint-Substitution)
  pp-Term                 = pp-Analysis(prettyprint-Term)
  pp-List(pp)             = pp-Analysis(prettyprint-list(pp))

rules

  irrelevant-constraint = fail
  irrelevant-pos = fail

  pp-analysis = qsort((pp-order,pp-order);lt) ; pp-Analysis(prettyprint-analysis)

  pp-order: (key,_) -> <get-index <+ !99> (key,[ "constraints"
                                               , "initial-scopegraph"
                                               , "initial-resolution"
                                               , "scopegraph"
                                               , "resolution"
                                               , "position-types"
                                               , "substitution"
                                               , "subtyping-relation"
                                               , "errors"
                                               , "warnings"
                                               , "notes"
                                               ])

  prettyprint-analysis: t -> [t']
    with t' := <pp-V-list(prettyprint-analysis-entry|"1")> t

  prettyprint-analysis-entry: e@(key,_) -> [ H([SOpt(HS(),"0")], [S(key),S(" := ")]) , e' ]
    with e' := <pp-indent(|"2")> [<pp-analysis-component> e]

  prettyprint-alternative-solution: e@(idx,s) -> [ H([SOpt(HS(),"0")], [S("["),S(<int-to-string> idx),S("] := "),s']) ]
    with s' := <pp-one-Z(prettyprint-analysis)> s

  pp-analysis-component = ?("constraints",<id>) ; filter(not(irrelevant-constraint)) ; pp-V-list(prettyprint-Constraint)
  pp-analysis-component = ?("initial-scopegraph",<id>) ; pp-one-Z(prettyprint-Graph)
  pp-analysis-component = ?("initial-resolution",<id>) ; pp-one-Z(prettyprint-Resolution)
  pp-analysis-component = ?("scopegraph",<id>)  ; pp-one-Z(prettyprint-Graph)
  pp-analysis-component = ?("resolution",<id>) ; pp-one-Z(prettyprint-RefDecl)
  pp-analysis-component = ?("subtyping-relation",<id>) ; pp-one-Z(prettyprint-SubtypingRelation)
  pp-analysis-component = ?("position-types",<id>) ; filter(not(irrelevant-pos)) ; pp-one-Z(prettyprint-PosType)
  pp-analysis-component = ?("substitution",<id>) ; pp-one-Z(prettyprint-Subst)
  pp-analysis-component = ?("errors",<id>) ; pp-one-Z(prettyprint-EditorMessages)
  pp-analysis-component = ?("warnings",<id>) ; pp-one-Z(prettyprint-EditorMessages)
  pp-analysis-component = ?("notes",<id>) ; pp-one-Z(prettyprint-EditorMessages)
  pp-analysis-component = ?("alternative-solutions",<id>) ; map-with-index(id) ; pp-V-list(prettyprint-alternative-solution|"1")
  pp-analysis-component = ?(key,<id>) ; with(<debug(!"prettyprint-analysis-component undefined for ")> key) ; pp-one-Z(prettyprint-Term)

