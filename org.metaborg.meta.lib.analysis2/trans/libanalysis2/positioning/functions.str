module libanalysis2/positioning/functions

imports
  runtime/editor/annotations
  runtime/editor/origins
  editor-common.generated

  libanalysis2/prelude/-
  signatures/libanalysis2/positioning/-
  signatures/libanalysis2/solver/-

rules
 
  annotate-keys =
    where(
      dr-begin-scope(|"NewKey");
      key-counter := <new-counter>;
      rules( NewKey: _ -> <next-counter> key-counter )
    );
    topdown(annotate-key);
    where(
      try(dr-end-scope(|"NewKey"))
    )

  annotate-key: t -> t{<NewKey>}
 
rules

  get-position = ?Var(_)
  get-position = ?DeclId(_,_,_)
  get-position = ?RefId(_,_,_)
  get-position: t{k} -> Pos(k,t)
  with <is-int> k <+ debug(!"ERROR: Cannot get key for ") ; fail

  position-to-term = ?Pos(_,<id>)
  position-to-term = ?DeclId(_,_,<position-to-term>)
  position-to-term = ?RefId(_,_,<position-to-term>)

  position-to-key = ?Pos(<id>,_)
  position-to-key = ?DeclId(_,_,<position-to-key>)
  position-to-key = ?RefId(_,_,<position-to-key>)

  copy-origin(|src): t -> <origin-location-offset-set(|o)> t
    where o := <origin-location-offset <+ debug(!"WARNING: Cannot get origin for ") ; !("",-1,-1,-1,-1)> src

rules
 
  apply-annotations(s|aa*): ast -> ast'
  with <topdown(try(apply-annotations-to-term(s|<mapk(position-to-key)> aa*)))> ast => ast'

  apply-annotations-to-term(s|aa*): t{a*} -> t{a''*}
  where a'* := <filter(<lookup> (<id>,aa*));map(s)> a*;
        a''* := [a*,a'*]
 