module libanalysis2/positioning/solver

imports
  runtime/editor/origins
  signatures/libanalysis2/-
  libanalysis2/positioning/-
  libanalysis2/unification/-
  libanalysis2/solver/-
  libanalysis2/prelude/-
  libanalysis2/pp
 
rules
 
  solve-constraint: (f@CTypeOf(p,ty,msginfo),sol-wm) -> [res]
  where <is-ground> p
  with <fromWM;sol-p> sol-wm => p*
     ; ( <lookup> (p, p*) => ty'
       < !(sol-wm, [CEqual(ty, ty', msginfo)])
       + !(<fmap(<sol-set-p(!ty)> (p,<id>))> sol-wm, [])
       ) => res

rules

  is-op: POfDecl(_) -> <id>
  is-op: POfRef(_) -> <id>

  reduce-op(|x*): POfDecl(DeclId(_, _, <id>)) -> <id>
  reduce-op(|x*): POfRef(RefId(_, _, <id>)) -> <id>

rules
 
  unsolved-message: CTypeOf(p,ty,msginfo) -> <default-message(|msg)> msginfo
  with msg := $[Could not assign type [<pp-Term> ty] to [<pp-Pos> p]]
