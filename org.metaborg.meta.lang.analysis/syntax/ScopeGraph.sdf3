module ScopeGraph

imports 

  Common

context-free syntax

  Section.ScopeGraph =
    <scope graph
    
      <Scopes>
      <Declarations>
      <References>
    >
  
context-free syntax
  
  Scopes =
    <scopes
      
      <{Scope "\n\n"}*>
    >
    
  Scope.Scope     = <<SID> <Parent>>
  Scope.Scope     = <<SID>:<ScopeRef> <Parent>>
  Scope.ScopeVar  = <var <SVar>>
  
  Parent.Global =
  Parent.Parent = [ -> [ScopeRef]]
  
  ScopeRef.SRef     = SID
  ScopeRef.SVarRef  = SVar
  
context-free syntax

  Declarations =
  	<declarations
  	 
  	 <{Declaration "\n\n"}*>
    >
  
  Declaration.Decl = <<Name> in <ScopeRef>>
  Declaration.Decl = <<Name>:<ScopeRef> in <ScopeRef>>

  Name.Name = <<STRING>_<INT>>
  
context-free syntax

  References =
  	<references
      
      <{Reference "\n\n"}*>
    >
    
  Reference.Ref    = <<Name> in <ScopeRef>>
  Reference.Import = <<Name>:<ScopeRef> in <ScopeRef>> 

lexical syntax

  SID   = "s" INT
  SVar  = "sigma" INT
  