module nabl/entries

imports
  
  nabl/collect
  nabl/utils
  nabl/uri
  nabl/resolve
  index/core
  editor/origins
  
signature

  constructors
    
    Def          : URI    -> Key
    Alias        : URI    -> Key
    InverseAlias : URI    -> Key
    Use          : Result -> Key

rules // Index entries to diff for incrementality
  
  nabl-diff-entry = ?Def(_)
  nabl-diff-entry = ?Alias(_)
 
rules
  
  new-def(|source, uri):
    x -> definition
    with
      definition := <origin-track-forced(!Def(uri))> x;
      <index-collect(|source)> definition;
			<new-alias(|source, <nabl-nonunique-uri> uri, uri)> x
      
  new-alias(|source, uri, alias-uri):
    _ -> alias
    with
      alias  := Alias(uri);
      ialias := InverseAlias(alias-uri);
      if <not(eq)> (uri, alias-uri) then
      	<index-collect(|alias-uri, source)> alias;
      	<index-collect(|uri, source)> ialias
      end

  new-use(|partition, uri):
    x -> use
    with
      use := <origin-track-forced(!Use(uri))> x

rules // index uri & value projections
  
  /** @internal */
  nabl-uri:
    Def(uri) -> uri

  /** @internal */
  nabl-uris:
    Def(uri) -> [uri]
    
  /** @internal */
  nabl-uri:
    Alias(uri) -> uri
    
  /** @internal */
  nabl-uris:
    Alias(uri) -> [uri]
    
  /** @internal */
  nabl-uri:
    InverseAlias(uri) -> uri
    
  /** @internal */
  nabl-uris:
    InverseAlias(uri) -> [uri]
    
  /** @internal */  
  nabl-uri:
    Use(uri) -> <nabl-collect-one-resolved-def; nabl-uri> uri
    
  /** @internal */  
  nabl-uris:
    Use(uri) -> <nabl-collect-all-resolved-defs; map(nabl-uri)> uri
