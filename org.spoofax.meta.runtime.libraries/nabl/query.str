module nabl/query

imports
  
  nabl/uri
  nabl/entries
  index/query
  task/core
  task/insert
  
rules // Specific queries

  /**
   * Gets all unique and non-unique definition entries that match the given URI.
   *
   * @type Def(uri) or "name"{uri} or URI(language, path) -> List(Def(uri))
   */
  nabl-get-all-definitions:
    contains-uri* -> <mapconcat(nabl-get-unique-nonunique(!Def(<id>)))> uri*
    where
    	uri* := <nabl-uris> contains-uri*
    	
  /**
   * Gets all child definition entries in given URI that match given namespace.
   *
   * @type Def(uri) or "name"{uri} or URI(language, path) -> List(Def(uri))
   */
  nabl-get-child-definitions(|namespace):
    contains-uri* -> def*
    where
    	uri* := <nabl-uris> contains-uri*;
    	def* := <mapconcat(!Def(<id>); nabl-get-all-childs(|namespace)); mapconcat(nabl-uri; nabl-get-unique-nonunique(!Def(<id>)))> uri*

  /**
   * Gets all Alias entries that match the given URI.
   *
   * @type Def(uri) or "name"{uri} or URI(language, path) -> List(aliasURI)
   */
  nabl-get-all-aliases:
    contains-uri* -> alias-uri*
    where
    	uri*           := <nabl-uris> contains-uri*;
    	nonunique-uri* := <filter(nabl-replace-uri-qualifier(|NonUnique()))> uri*;
      alias-uri*     := <mapconcat(!Alias(<id>); index-get-all-values)> nonunique-uri*
      
  /**
   * Gets all InverseAlias entries that match the given URI.
   *
   * @type Def(uri) or "name"{uri} or URI(language, path) -> List(inverseAliasURI)
   */
  nabl-get-all-inverse-aliasses:
    contains-uri* -> inverse-alias*
    where
    	uri*           := <nabl-uris> contains-uri*;
      inverse-alias* := <mapconcat(!InverseAlias(<id>); index-get-all-values)> uri*

rules // Generic queries
  
  nabl-get-all-childs:
    key -> entries
      with
        entries  := <index-get-all-childs> key

  nabl-get-all-childs(|namespace):
    key -> entries
      with
        children := <index-get-all-childs> key;
        entries  := <nabl-filter-entries(|namespace)> children
        
  nabl-get-all-childs(|namespace, prefix):
    key -> entries
      with
        children := <index-get-all-childs> key;
        entries  := <nabl-filter-entries(|namespace, prefix)> children

  nabl-get-unique-nonunique(create-key):
  	uri -> [unique*, non-unique*]
  	where
      if alias* := <nabl-get-all-aliases> uri then
      	// HACK: handle Result terms on the resulting URI of an alias. this should be decomposed into a task?
      	(alias-result*, alias-uri*) := <partition(?Result(_); insert-results-or-delay)> alias*;
      	unique-uri*                 := <mapconcat(create-key; index-get-all-values)> alias-uri*;
      	unique-result*              := <try(concat)> alias-result*;
      	unique*                     := [unique-uri*, unique-result*]
      else
        unique* := []
      end;
      if non-unique-uri := <nabl-replace-uri-qualifier(|NonUnique())> uri then
        non-unique* := <create-key; index-get-all-values> non-unique-uri
      else
        non-unique* := []
      end

rules /** @internal Entry filtering */
  
  /** @internal */
  nabl-filter-entries(|namespace, prefix):
    entry* -> <filter(nabl-compare-prefix(|namespace, prefix))> entry*
  
  /** @internal */
  nabl-filter-entries(|namespace):
    entry* -> <filter(nabl-compare-namespace(|namespace))> entry*

rules /** @internal Helpers */
  
  /** @internal */
  nabl-compare-prefix(|namespace, prefix):
    entry -> <id>
    where
      uri := <nabl-uri> entry;
      <eq> (namespace, <nabl-uri-namespace> uri);
      <is-substring(!prefix)> <nabl-uri-name> uri
      
  /** @internal */
  nabl-compare-namespace(|namespace):
    entry -> <id>
    where
      uri := <nabl-uri> entry;
      <eq> (namespace, <nabl-uri-namespace> uri)
